plugins {
    id 'idea'
    alias libs.plugins.cursegradle
    alias libs.plugins.minotaur
}

apply plugin: libs.plugins.quilt.loom.get().pluginId

base {
    archivesName = "${mod_id}-quilt-${libs.versions.minecraft.get()}"
}

sourceSets {
    testmod {
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

loom {
    runs {
        client {
            client()
            setConfigName("Quilt Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Quilt Server")
            ideConfigGenerated(true)
            runDir("run-server")
        }
    }
    runs.each {
        it.source sourceSets.testmod
        it.vmArgs '-Dnet.fabricmc.tinyremapper.knownindybsm=org/codehaus/groovy/vmplugin/v8/IndyInterface'
    }
}

dependencies {
    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
    }

    modImplementation libs.quilt.loader

    modImplementation libs.qfapi

    modImplementation(libs.cgl.quilt)

    runtimeOnly(libs.groovyduvet.groovy)
    modImplementation(libs.groovyduvet.all)

    modImplementation(include(libs.scriptresources.quilt.get())) {
        exclude group: 'io.github.lukebemish.groovyduvet'
    }

    compileOnly(project(path: ":Common", configuration: "namedElements")) {
        exclude group: 'org.quiltmc'
    }
    testmodImplementation sourceSets.main.output
}

compileGroovy {
    source project(":Common").sourceSets.main.allGroovy
}

compileJava {
    source project(":Common").sourceSets.main.allJava
}

processResources {
    from project(':Common').sourceSets.main.resources
    exclude 'mods.groovy'
}

processTestmodResources {
    from project(':Common').sourceSets.test.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId base.archivesName.get()
            version = project.version
            from components.java
        }
    }
}


if (System.getenv('CURSEFORGE_KEY')) {
    curseforge {
        apiKey = System.getenv('CURSEFORGE_KEY')
        project {
            id = '873765'
            releaseType = "${project.release_type}"
            addGameVersion libs.versions.minecraft.get()
            addGameVersion 'Quilt'
            mainArtifact(remapJar) {
                displayName = "WhatsUp [Quilt] v$project.version"
            }
            relations {
                requiredDependency 'groovyduvet'
                requiredDependency 'qsl'
            }
        }
        options {
            forgeGradleIntegration = true
        }
    }

    tasks.curseforge {
        dependsOn remapJar
    }
}

if (System.getenv('MODRINTH_KEY')) {
    modrinth {
        token = System.getenv('MODRINTH_KEY')
        projectId = '7t2kydgp'
        versionNumber = "$project.version"
        versionType = "${project.release_type}"
        detectLoaders = false
        uploadFile = remapJar

        gameVersions = [libs.versions.minecraft.get()]
        loaders = ['quilt']
        dependencies {
            required.project 'groovyduvet'
            required.project 'qsl'
        }
    }

    tasks.modrinth {
        dependsOn remapJar
    }
}
